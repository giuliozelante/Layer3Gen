plugins {
	id 'java'
	id 'idea'
	id 'java-gradle-plugin'
	id 'maven-publish'
	id 'application'
	id 'com.gradle.plugin-publish' version '1.0.0'
	id "com.github.ben-manes.versions" version "0.42.0"
}

group = 'com.github.giuliozelante'
version = '1.8'

repositories {
	mavenCentral()
}

dependencies {

	implementation 'org.yaml:snakeyaml:1.30' // parse yml
	implementation 'org.slf4j:slf4j-api:1.7.36' // logging
	implementation 'org.slf4j:slf4j-log4j12:1.7.36'
	implementation 'com.google.guava:guava:31.1-jre' // needed but don't know why
	implementation 'org.apache.commons:commons-lang3:3.12.0' // utils

	implementation 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final' // @Entity class needed
	implementation 'org.reflections:reflections:0.10.2' // read classes
	implementation 'org.freemarker:freemarker:2.3.31'// write classes

	compileOnly 'org.projectlombok:lombok:1.18.24' // autogenerate setter getter etc
	compileOnly gradleApi() // gradle plugin

	annotationProcessor "org.mapstruct:mapstruct-processor:1.5.2.Final"
	annotationProcessor 'org.projectlombok:lombok:1.18.24'

	testImplementation 'junit:junit:4.13.2'
}

gradlePlugin {
	plugins {
		simplePlugin {
			id = 'com.github.giuliozelante.layer3gen'
			displayName = 'Layer 3 Gen'
			description = 'Generates Spring standard 3 layer classes (Controller, Service, Repository) starting from JPA entities'
			implementationClass = 'com.github.giuliozelante.layer3gen.gradle.Layer3GenPlugin'
		}
	}
}

pluginBundle {
	website = 'https://github.com/giuliozelante/Layer3Gen'
	vcsUrl = 'https://github.com/giuliozelante/Layer3Gen'
	tags = ['jpa', 'code-generation', 'spring', 'spring-data']
}

application {
	mainClass = 'com.github.giuliozelante.layer3gen.main.Layer3GenMain'
}

distZip {
	into(project.name + '-' + project.version + '/bin') {
		from './conf'
		include '*'
	}
	exclude 'gradle-api*.jar'
}

installDist {
	into('bin') {
		from './conf'
		include '*'
	}
	exclude 'gradle-api*.jar'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}